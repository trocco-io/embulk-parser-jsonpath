plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id "java"
    id "checkstyle"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.4.2"
}
repositories {
    mavenCentral()
    jcenter()
}

group = "com.github.hiroyuki_sato"
version = "0.3.3"
description = "Parses JSON files with JSONPath read by other file input plugins."

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly "org.embulk:embulk-core:0.9.23"

    compile('com.jayway.jsonpath:json-path:2.4.0') {
        exclude group: "org.slf4j", module: "slf4j-api"
    }

    testCompile "junit:junit:4.+"
    testCompile  "org.embulk:embulk-core:0.9.23:tests"
    testCompile  "org.embulk:embulk-standards:0.9.23"

    // TODO: Remove them.
    // These `testCompile` are a tentative workaround. It will be covered in Embulk core's testing mechanism.
    testCompile "org.embulk:embulk-deps-buffer:0.9.23"
    testCompile "org.embulk:embulk-deps-config:0.9.23"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

embulkPlugin {
    mainClass = "org.embulk.parser.jsonpath.JsonpathParserPlugin"
    category = "parser"
    type = "jsonpath"
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    repositories {
        maven {
            url = "${project.buildDir}/mavenPublishLocal"
        }
    }
}

gem {
    authors = [ "Hiroyuki Sato","Takuma kanari" ]
    email = [ "hiroysato@gmail.com","chemtrails.t@gmail.com"]
    // "description" of the gem is copied from "description" of your Gradle project.
    summary = "JSON parser with JSONPath plugin for Embulk"
    homepage = "https://github.com/hiroyuki-sato/embulk-parser-jsonpath"
    licenses = [ "MIT" ]

    generateRubyCode = false  // Avoiding generate lib/embulk/parser/jsonpath.rb
    into("lib/embulk/parser/") {
      from "lib/embulk/parser/jsonpath.rb"
    }
    into("lib/embulk/guess/") {
      from "lib/embulk/guess/jsonpath.rb"
    }
    dependencies = [
      "'jsonpath', ['~> 0.5']",
      "'json', ['~> 2.0', '>= 2.0']"
    ]
}

gemPush {
    host = "https://rubygems.org"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}


// For avoiding the following exception
// Caused by:
// java.lang.OutOfMemoryError: Java heap space
test {
  maxHeapSize = "2g"
}
